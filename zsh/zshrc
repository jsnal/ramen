# Global array

typeset -A _ramen
_ramen[ZSH_HOME]="$HOME/.zsh"

# Inlcude plugins and utils
source $_ramen[ZSH_HOME]/utils/colors.zsh
source $_ramen[ZSH_HOME]/utils/time.zsh
source $_ramen[ZSH_HOME]/.zshrc.local

# Aliases
alias v='nvim'
alias vi='nvim'
alias vim='nvim'
alias ls='ls --color=always'
alias l='ls'
alias cp='cp -v'
alias man='nocorrect man'
alias mkdir='nocorrect mkdir'
alias mv='nocorrect mv'
alias sudo='nocorrect sudo'
alias docker='nocorrect docker'

if [ "$(uname 2> /dev/null)" = "Linux" ]; then
  alias ls='ls --color=always --group-directories'
  alias ll='ls -la --color=always --group-directories'
fi

# Binds

bindkey -e
bindkey '^b' backward-word
bindkey '^d' kill-whole-line
bindkey '^j' kill-word

# Completion

autoload -Uz compinit
autoload -U +X bashcompinit && bashcompinit
compinit

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format ''
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=2
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SList: %p%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' menu select
zstyle ':completion:*' select-prompt %SList: %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*:history-words' stop yes
zstyle ':completion:*:history-words' remove-all-dups yes
zstyle ':completion:*:history-words' list false
zstyle ':completion:*:history-words' menu yes
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:*kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

# Options

setopt append_history
setopt extended_history
setopt sharehistory
setopt hist_reduce_blanks
setopt always_to_end
setopt complete_in_word
setopt auto_menu
setopt correct
setopt correctall
setopt list_packed

# Prompt
function preexec {
  timer=${timer:-$SECONDS}
  export ZSH_COMMAND_TIME=""
}

function precmd {
  _ramen[EXIT_CODE]=$?

  if [ $timer ]; then
    _ramen[COMMAND_TIME]=$(($SECONDS - $timer))
    export ZSH_COMMAND_TIME="$_ramen[COMMAND_TIME]"
    zsh_command_time
    unset timer
  fi

  local USER="$_ramen[YELLOW]%n$_ramen[WHITE]@$_ramen[GREEN_YELLOW]%m"
  local DIR="$_ramen[BLUE]%~"

  # Paint the USER red if the user is root
  [ $UID -eq 0 ] && local USER="$_ramen[WHITE]%n@%m"

  # Paint USER red if command fails
  [ ! $_ramen[EXIT_CODE] -eq 0 ] && local USER="$_ramen[RED]%n@%m"

  export PROMPT="$_ramen[BOLD_START]$USER$_ramen[WHITE]:$DIR$_ramen[WHITE]$_ramen[BOLD_END]%# "
  export RPROMPT="$_ramen[GRAY]$_ramen[COMMAND_TIME]$_ramen[WHITE]"

  # Set window title in xterm
  case $TERM in
    xterm*|rxvt*) print -Pn "\e]0;%n@%m:%~\a" ;;
    *) ;;
  esac
}

export SPROMPT="%B%F{red}'%R'%b%f -> %B%F{red}'%r'%b%f [%B%Uy%u%bes, %B%Un%u%bo, %B%Ue%u%bdit, %B%Ua%u%bbort]? "
